plugins {
    id 'application'
    id 'jacoco'
}

repositories {
    jcenter()
}

dependencies {
    implementation "io.reactivex.rxjava3:rxjava:3.0.7"
    compile group: 'org.javassist', name: 'javassist', version: '3.27.0-GA'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.10.0.202012080955-r'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    implementation 'com.google.guava:guava:29.0-jre'
}

application {
    mainClass = 'org.matrixer.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes("Manifest-Version": "1.0",
                "Agent-Class": "org.matrixeragent.MatrixerAgent",
                "Can-Redefine-Classes": true,
                "Can-Retransform-Classes": true,
                "Premain-Class": "org.matrixeragent.MatrixerAgent")
    }
    baseName 'agentJar'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it)}
    }
    doLast {
        println "default jar task run"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

// Generate jar of CustomTestAgent
task testAgentJar(type: Jar) {
    from (sourceSets.test.output) {
        include "org/matrixeragent/util/**"
    }
    manifest {
        attributes("Manifest-Version": "1.0",
                "Agent-Class": "org.matrixeragent.util.CustomTestAgent",
                "Can-Redefine-Classes": true,
                "Can-Retransform-Classes": true,
                "Premain-Class": "org.matrixeragent.util.CustomTestAgent")
    }
    baseName 'testAgentJar'
    doLast {
        println "testAgentJar run"
    }
}

test {
    dependsOn testAgentJar
    jvmArgs = ["-Djdk.attach.allowAttachSelf=true", "-javaagent:build/libs/agentJar.jar=arg1:org.matrixeragent.statictargets"]
    testLogging {
        events 'passed', 'skipped', 'failed'  // Events to print out

        showStackTraces false
        showExceptions true
        showCauses true
        exceptionFormat 'full'

        showStandardStreams true
    }
    jacoco {
        excludes = [
                'org.matrixeragent.*'
        ]
    }
    // Update the jacoco report after tests, this might be removed can
    // be removed if it takes too long to run. But it is handy to have
    // it automatic at the moment
    finalizedBy jacocoTestReport
}

/*
 * When the build task is executed jacoco generates a binary report.
 * This task generates a human readable report (currently HTML)
 * Run with 'gradlew jacocoTestReport' it will automatically run the tests
 * if they need to be run.
 */
jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHTML")
    }
}

/*
 * This task verifies that the project adheres to the specified coverage
 * rules. Will be run after the check task that is executed with build or test
 */
jacocoTestCoverageVerification {
    violationRules {
        rule {
            excludes = [
                    'org.matrixer.App',
                    'org.matrixeragent.*'

            ]
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8  // 100% test coverage requiered globally
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification
