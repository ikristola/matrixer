plugins {
    id 'java-library'
}

repositories {
    jcenter()
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("com.matrixer:core") with project(':core')
    }
}

dependencies {
    implementation 'com.matrixer:core'
    implementation "io.reactivex.rxjava3:rxjava:3.0.7"
    compile group: 'org.javassist', name: 'javassist', version: '3.27.0-GA'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    implementation 'com.google.guava:guava:29.0-jre'
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes("Manifest-Version": "1.0",
                "Agent-Class": "org.matrixer.agent.MatrixerAgent",
                "Can-Redefine-Classes": true,
                "Can-Retransform-Classes": true,
                "Premain-Class": "org.matrixer.agent.MatrixerAgent")
    }
    baseName 'agentJar'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it)}
    }
    doLast {
        println "default jar task run"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

// Generate jar of CustomTestAgent
task testAgentJar(type: Jar) {
    from (sourceSets.test.output) {
        include "org/matrixeragent/util/**"
    }
    manifest {
        attributes("Manifest-Version": "1.0",
                "Agent-Class": "org.matrixer.agent.util.CustomTestAgent",
                "Can-Redefine-Classes": true,
                "Can-Retransform-Classes": true,
                "Premain-Class": "org.matrixer.agent.util.CustomTestAgent")
    }
    baseName 'testAgentJar'
    doLast {
        println "testAgentJar run"
    }
}

test {
    dependsOn assemble
    dependsOn testAgentJar
    jvmArgs = ["-Djdk.attach.allowAttachSelf=true",
    "-javaagent:build/libs/agentJar.jar=${buildDir}:org.matrixer.agent.statictargets"]
    testLogging {
        events 'passed', 'skipped', 'failed'  // Events to print out

        showStackTraces true
        showExceptions true
        showCauses true
        exceptionFormat 'full'

        showStandardStreams true
    }
    // Update the jacoco report after tests, this might be removed can
    // be removed if it takes too long to run. But it is handy to have
    // it automatic at the moment
}
