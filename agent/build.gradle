plugins {
    id 'java-library'
    id 'matrixer-jacoco'
}

dependencies {
    implementation project(':core')
    compile group: 'org.ow2.asm', name:'asm', version:'9.1'
    compile group: 'org.ow2.asm', name:'asm-util', version:'9.1'
    compile group: 'org.ow2.asm', name:'asm-commons', version:'9.1'
    implementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.8.0-M1'
    testImplementation group: 'org.testng', name: 'testng', version: '6.8'
}

jar {
    manifest {
        attributes("Manifest-Version": "1.0",
                "Agent-Class": "org.matrixer.agent.MatrixerAgent",
                "Can-Redefine-Classes": true,
                "Can-Retransform-Classes": true,
                "Premain-Class": "org.matrixer.agent.MatrixerAgent")
    }
    archiveFileName =  'agentJar.jar'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it)}
    }
    from {
        files("${project(':core').buildDir}/classes/java/main/")
    }
    exclude('org/matrixer/core/util')
    exclude('org/matrixer/core/*.class')
    doLast {
        println "default jar task run"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

// Generate jar of CustomTestAgent
task testAgentJar(type: Jar) {
    from (sourceSets.test.output) {
        include "org/matrixeragent/util/**"
    }
    manifest {
        attributes("Manifest-Version": "1.0",
                "Agent-Class": "org.matrixer.agent.util.CustomTestAgent",
                "Can-Redefine-Classes": true,
                "Can-Retransform-Classes": true,
                "Premain-Class": "org.matrixer.agent.util.CustomTestAgent")
    }
    archiveFileName = 'testAgentJar.jar'
    doLast {
        println "testAgentJar run"
    }
}

test {
    dependsOn assemble // Makes sure the jars are assembled before tests
    dependsOn testAgentJar
    // TODO use variables for arguments to make it easier to read/maintain
    jvmArgs += ["-Djdk.attach.allowAttachSelf=true",
    "-javaagent:build/libs/agentJar.jar=destfile=${buildDir}/matrixer-results.txt,pkg=org.matrixer.agent.statictargets,testPkg=org.matrixer.agent"]
}
